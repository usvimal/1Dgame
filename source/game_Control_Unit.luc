module game_Control_Unit (
    input clk,  // clock
    input rst,  // reset
    input left_button,
    input right_button,
    
    input player_position_in[16],
    input ra_data[16],
    input player_row[16],
    input player_level[16],
    
    output alufn[6],
    output asel[3],
    output bsel[3],
    output wdsel[2],
    output we_regfile,
    output regfile_write_address[4],
    output regfile_read_address_a[4],
    output regfile_read_address_b[4],
    output decimal_counter_increase,
    output screen_output[64][24],
    output mazeLastRow[16],
    
    //DEBUG
    output state_number[8]
  )
  
  {
  const LEVEL1_SIZE = 26;
  const LEVEL2_SIZE = 25;
  const LEVEL3_SIZE = 24;
  const PAUSE_SIZE = 28;
  
  const ADD = b000000;
  const SUB = b000001;
  const MUL = b000010;
  const DIV = b000011;
  const NOTA = b010000;
  const NOTB = b010010;
  const XNOR = b010100;
  const XOR = b010110;
  const AND = b011000;
  const A = b011010;
  const B = b011100;
  const OR = b011110;
  const SHL = b100000;
  const SHR = b100001;
  const SRA = b100011;
  const CMPEQ = b110011;
  const CMPLT = b110101;
  const CMPLE = b110111;
  const REDXOR = b010111;
  
  
  .clk(clk){
    fsm main_game={
      IDLE,
      START,
      CHECK_LEFT,
      CHECK_RIGHT,
      BRANCH_LEFT,
      BRANCH_RIGHT,
      PLAYER_LEFT,
      PLAYER_RIGHT,
      COIN_COLLISION,
      CHECK_COIN,
      BRANCH_COIN,
      LEFT_SHIFT_8, // for obstacle check
      OBSTACLE_COLLISION,
      CHECK_OBSTACLE,
      BRANCH_OBSTACLE,
      ROW_CHECK,
      ROW_BRANCH,
      LEVEL_CHECK,
      LEVEL_BRANCH,
      INTERMISSION_CHECK,
      NEXT_ROW,
      NEXT_LEVEL,
      RESET_ROW,
      PAUSE,
      CLEAR_PLAYER,
      GAME_OVER_SCROLL,
      SCROLL_CHECK,
      SCROLL_BRANCH
    };
    edge_detector slowClockEdgeLevel1(#RISE(1), #FALL(0));
    edge_detector slowClockEdgeLevel2(#RISE(1), #FALL(0));
    edge_detector slowClockEdgeLevel3(#RISE(1), #FALL(0));
    edge_detector slowClockPauseEdge(#RISE(1), #FALL(0));
    
    .rst(rst){
      rom rom;
      rom_decoder rom_decoder;
      counter slowClockLevel1(#SIZE(LEVEL1_SIZE));
      counter slowClockLevel2(#SIZE(LEVEL2_SIZE));
      counter slowClockLevel3(#SIZE(LEVEL3_SIZE));
      counter slowClockPause(#SIZE(PAUSE_SIZE));
      
    }
  }
  
  always {
    // Get slowclock edges for the various levels
    slowClockEdgeLevel1.in = slowClockLevel1.value[LEVEL1_SIZE-1];
    slowClockEdgeLevel2.in = slowClockLevel2.value[LEVEL2_SIZE-1];
    slowClockEdgeLevel3.in = slowClockLevel3.value[LEVEL3_SIZE-1];
    slowClockPauseEdge.in = slowClockPause.value[PAUSE_SIZE-1];
    
    // standard setting unless otherwise overwritten by each case 
    alufn = 0;
    asel = 0;
    bsel = 0;
    wdsel = 0;
    we_regfile = 0;
    regfile_write_address = b1111;
    regfile_read_address_a = b1111;
    regfile_read_address_b = b0000;
    
    // rom decoder inputs & outputs
    rom_decoder.rom_maze = rom.out;
    rom_decoder.player_pos = player_position_in;
    screen_output = rom_decoder.out;
    mazeLastRow = rom.out[0];
    
    // rom input
    rom.player_row = player_row;
    rom.player_level = player_level;
    
    decimal_counter_increase = 0;    
    
    //debug
    state_number = b00000000;
    
    if (rst){
      main_game.d = main_game.IDLE;
    }
    
    case(main_game.q){
      
      main_game.IDLE:
        state_number = b00000001;
        
        if (left_button | right_button) {  // wait for player input to start the game
          main_game.d = main_game.START;
        }
        
        else {
          main_game.d = main_game.IDLE;
        }
      
      main_game.START:
        regfile_write_address = b0101;
        we_regfile =1;
        wdsel = b11;
        state_number = b00000010;
        
        if (left_button) {  // check if player can be moved to left
          main_game.d = main_game.CHECK_LEFT;
        }
        
        if (right_button) {  // check if player can be moved to right
          main_game.d = main_game.CHECK_RIGHT;
        }
        
        //check for coin collision and obstacle collision depending on slowclock
        if (player_level == 1 && slowClockEdgeLevel1.out == b1) {      
          main_game.d = main_game.COIN_COLLISION;}
        
        if (player_level == 3 && slowClockEdgeLevel2.out == b1) {
          main_game.d = main_game.COIN_COLLISION;}
        
        if (player_level == 5 && slowClockEdgeLevel3.out == b1) {
          main_game.d = main_game.COIN_COLLISION;}
        
        if (player_level == 6 && slowClockEdgeLevel2.out == b1) {
          main_game.d = main_game.ROW_CHECK;}
      
      
      
      
      
      main_game.CHECK_LEFT:                       // output 0 if player position at left edge
        alufn = CMPLT;
        regfile_read_address_a = b100;            // player position
        asel= b00;                                // Read Address A
        bsel = b10;                               // 10000000 in binary
        we_regfile= 1;
        regfile_write_address= b1111;             // temp var
        wdsel = b00;
        state_number = b00000011;
        
        main_game.d = main_game.BRANCH_LEFT;
      
      main_game.CHECK_RIGHT:                      // output 0 if player position at right edge
        alufn = CMPLT;
        regfile_read_address_b = b100;            // player position
        asel= b01;                                // b1
        bsel = b00;                               // Read Address B
        we_regfile= 1;                            
        wdsel = b00;
        regfile_write_address= b1111;             // temp var
        state_number = b00000100;
        
        main_game.d = main_game.BRANCH_RIGHT;
      
      main_game.BRANCH_LEFT:                      // make sure player does not move out of screen by not shifting player position at left edge
        regfile_read_address_a = b1111;
        state_number = b00000101;          
        if (ra_data[0]) {
          main_game.d = main_game.PLAYER_LEFT;
        }
        else {main_game.d = main_game.START;}
      
      main_game.BRANCH_RIGHT:                    // make sure player does not move out of screen by not shifting player position at right edge
      state_number = b00000110;
        regfile_read_address_a = b1111;          // temp var
        if (ra_data[0]) {
          main_game.d = main_game.PLAYER_RIGHT;
        }
        
        else {main_game.d = main_game.START;}
      
      main_game.PLAYER_LEFT:                         // shift player position to the left
        alufn = SHL;                                 
        regfile_read_address_a = b0100;              // player position
        asel = b00;                                  // Read Address A
        bsel = b01;                                  // constant 1
        we_regfile = 1;                              
        wdsel = b00;
        regfile_write_address = b0100;               // player position
        state_number = b00000111;
        
        main_game.d = main_game.START;
      
      
      main_game.PLAYER_RIGHT:                        // shift player position to the right
        alufn = SHR;                             
        regfile_read_address_a = b0100;              // player position
        asel = b00;                                  // Read Address A
        bsel = b01;                                  // constant 1
        we_regfile = 1;                              // enable write
        wdsel = b00;
        regfile_write_address = b0100;               //counter reg
        state_number = b00001000;
        
        main_game.d = main_game.START;
      
      main_game.COIN_COLLISION:                       // perform AND operation to check for overlaps between player and coin
        alufn = AND;
        asel= b00;                                    // Read Address A
        bsel = b00;                                   // Read Address B
        we_regfile= 1;                                
        regfile_write_address= b1111;                 // temp var
        regfile_read_address_a = b0100;               // player position
        regfile_read_address_b = b0101;               // mazeLastRow
        wdsel = b00;
        state_number = b00001001;
        
        main_game.d = main_game.CHECK_COIN;
      
      main_game.CHECK_COIN:                          // output 1 if player collides with coin
        alufn = REDXOR;
        asel= b00;                                   // Read Address A
        we_regfile= 1;                               // enable write
        regfile_write_address= b1111;                // temp var
        regfile_read_address_a = b1111;              // temp var
        wdsel = b00;
        state_number = b00001010;
        
        main_game.d = main_game.BRANCH_COIN;
      
      main_game.BRANCH_COIN:                         // increments 7seg if coin was collected
        regfile_read_address_a = b1111;              // temp var
        state_number = b00001011;
        if (ra_data[0]) {decimal_counter_increase = 1;}
        
        main_game.d = main_game.LEFT_SHIFT_8;
      
      main_game.LEFT_SHIFT_8:                           // shift player position by 8 to compare with obstacles
        alufn = SHL;
        asel = b00;                                     // Read Address A
        bsel = b11;                                     // constant 8
        we_regfile= 1;                                  
        regfile_write_address= b1111;                   // temp var
        regfile_read_address_a = b0100;                 // player position
        wdsel = b00;
        state_number = b00001100;
        
        main_game.d = main_game.OBSTACLE_COLLISION;
      
      
      main_game.OBSTACLE_COLLISION:                     // perform AND operation to check for overlaps between player and obstacle
        alufn = AND;
        asel = b00;                                     // Read Address A
        bsel = b00;                                     // Read Address B
        we_regfile= 1;                                  
        regfile_write_address = b1111;                  // temp var
        regfile_read_address_a = b1111;                 // temp var
        regfile_read_address_b = b0101;                 // mazeLastRow
        wdsel = b00;
        state_number = b00001101;
        
        main_game.d = main_game.CHECK_OBSTACLE;
      
      main_game.CHECK_OBSTACLE:                         // output 1 if player collides with obstacle
        alufn = REDXOR;
        asel = b00;                                     // Read Address A
        we_regfile = 1;                                 // enable write
        regfile_write_address = b1111;                  // temp var
        regfile_read_address_a = b1111;                 // temp var
        wdsel = b00;
        state_number = b00001110;
        
        main_game.d = main_game.BRANCH_OBSTACLE;
      
      
      main_game.BRANCH_OBSTACLE:                       // skip to next level if obstacle collided, else continue iterating row
        regfile_read_address_a = b1111;                // temp var
        state_number = b00001111;
        if (ra_data[0]) {main_game.d = main_game.LEVEL_CHECK;}
        else {main_game.d = main_game.ROW_CHECK;}
      
      main_game.ROW_CHECK:                             // make sure row doesnt exceed when displaying on screen
        alufn = CMPLE;
        asel= b11;                                     // constant 48
        bsel = b00;                                    // Read Address B
        we_regfile= 1;
        regfile_write_address= b1111;                  // temp var
        regfile_read_address_b = b0010;                // player level
        wdsel = b00;
        state_number = b00010000;
        
        main_game.d = main_game.ROW_BRANCH;
      
      main_game.ROW_BRANCH:                            // if no more rows left to iterate, proceed to next level, else iterate next row
        regfile_read_address_a = b1111;                // temp var
        state_number = b00010001;
        if (ra_data[0]) {main_game.d = main_game.LEVEL_CHECK;}
        else {main_game.d = main_game.NEXT_ROW;}
      
      main_game.LEVEL_CHECK:                            // Make sure level < 6
        alufn = CMPLE;
        asel = b10;                                     // constant 6
        bsel = b00;                                     // Read Address B
        we_regfile= 1;
        regfile_write_address = b1111;                  // temp var
        regfile_read_address_b = b0011;                 // player level
        wdsel = b00;
        state_number = b00010010;
        
        main_game.d = main_game.LEVEL_BRANCH;
      
      main_game.LEVEL_BRANCH:                           // proceed to next level if level <6, else end the game
        regfile_read_address_a = b1111;                 // temp var
        state_number = b00010011;
        if (ra_data[0]) {main_game.d = main_game.IDLE;}
        else {main_game.d = main_game.NEXT_LEVEL;}
      
      main_game.NEXT_ROW:                                // iterate next row to be displayed on screen
        alufn = ADD;
        asel= b00;                                       // Read Address A
        bsel = b01;                                      // constant 1
        we_regfile = 1;
        regfile_write_address = b0010;                   // player row  
        regfile_read_address_a = b0010;                  // player row
        wdsel = b00;
        state_number = b00010100;
        
        main_game.d = main_game.START;
      
      main_game.NEXT_LEVEL:                               // iterate to next level for player_level
        alufn = ADD;
        asel= b00;                                        // Read Address A
        bsel = b01;                                       // constant 1
        we_regfile = 1;
        regfile_write_address = b0011;                    // player level
        regfile_read_address_a = b0011;                   // player level
        wdsel = b00;
        state_number = b00010101;
        
        main_game.d = main_game.RESET_ROW;
      
      main_game.RESET_ROW:                                // reset to 1st row for new level
        we_regfile = 1;
        regfile_write_address = b0010;                    // player row
        wdsel = b01;                                      // constant 1
        state_number = b00010110;
        
        main_game.d = main_game.INTERMISSION_CHECK;
      
      
      main_game.INTERMISSION_CHECK:
      state_number = b00010111;
        case(player_level) {
          2: main_game.d = main_game.PAUSE;
          4: main_game.d = main_game.PAUSE;
          3: main_game.d = main_game.START;
          5: main_game.d = main_game.START;
          6: main_game.d = main_game.CLEAR_PLAYER;
        }
      
      main_game.PAUSE:
      state_number = b00011000;
        if (slowClockPauseEdge.out == b1) {              // proceed to next level on slowclock edge
          main_game.d = main_game.NEXT_LEVEL;
        }
      
      main_game.CLEAR_PLAYER:                            // set player position to 0, deleting player
        we_regfile = 1;
        regfile_write_address = b0100;                   // player position
        wdsel = b01;
        state_number = b00011001;
        
        if (slowClockPauseEdge.out == b1) {
          main_game.d = main_game.ROW_CHECK;
        }
      
      
    }
  }
}